import org.jlleitschuh.gradle.ktlint.reporter.ReporterType

plugins {
    kotlin("jvm") version "1.9.25"
    kotlin("plugin.spring") version "1.9.25"
    kotlin("plugin.jpa") version "1.9.25"
    id("org.springframework.boot") version "3.5.3"
    id("io.spring.dependency-management") version "1.1.4"
    id("com.google.devtools.ksp") version "1.9.25-1.0.20"
    id("org.jlleitschuh.gradle.ktlint") version "13.0.0"
}

group = "com.example"
version = "0.0.1-SNAPSHOT"
description = "spring-boot-on-kotlin"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

// querydsl ë²„ì „ ë§¤í•‘
val queryDslVersion = "7.0"
extra["queryDslVersion"] = queryDslVersion

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("com.fasterxml.jackson.module:jackson-module-kotlin")
    implementation("org.jetbrains.kotlin:kotlin-reflect")
    implementation("org.springframework.boot:spring-boot-configuration-processor")
//    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
//    implementation("com.mysql:mysql-connector-j")
//    implementation("org.springframework.boot:spring-boot-starter-data-redis")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testRuntimeOnly("org.junit.platform:junit-platform-launcher")
}

// kotlin ì»´íŒŒì¼ ê´€ë ¨ ì˜µì…˜
pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
    kotlin {
        jvmToolchain(21)
        compilerOptions {
            freeCompilerArgs.addAll(
                "-Xjsr305=strict",
                "-opt-in=kotlin.RequiresOptIn",
            )
        }
    }
}

tasks.withType<Test> {
    jvmArgs("-XX:+EnableDynamicAgentLoading")
    useJUnitPlatform()
}

// ktlint ê´€ë ¨ ì„¤ì •
tasks.register("installKtlintGitPreCommitHook") {
    group = "verification"
    description = "Install git pre-commit hook for ktlint check only"

    val hooksDirectory = file(".git/hooks")
    val preCommitFile = hooksDirectory.resolve("pre-commit")

    doLast {
        if (!hooksDirectory.exists()) {
            hooksDirectory.mkdirs()
        }

        preCommitFile.writeText(
            """
            #!/bin/sh
            echo "ğŸ” Running ktlint check before commit..."
            ./gradlew ktlintCheck
            
            if [ ${'$'}? -ne 0 ]; then
                echo "âŒ ktlint check failed!"
                echo "ğŸ’¡ Please run './gradlew ktlintFormat' to fix formatting issues"                
                exit 1
            fi
            
            echo "âœ… ktlint check passed!"
            """.trimIndent(),
        )

        preCommitFile.setExecutable(true)
        println("âœ… Git pre-commit hook installed at ${preCommitFile.absolutePath}")
    }
}

// ì„œë¸Œí”„ë¡œì íŠ¸ì—ì„œ ì œê±°í•˜ê³  ë£¨íŠ¸ì—ì„œë§Œ ê´€ë¦¬
tasks.register("uninstallKtlintGitPreCommitHook") {
    group = "verification"
    description = "Remove git pre-commit hook"

    val preCommitFile = file(".git/hooks/pre-commit")

    doLast {
        if (preCommitFile.exists()) {
            preCommitFile.delete()
            println("ğŸ—‘ï¸ Git pre-commit hook removed")
        } else {
            println("â„¹ï¸ No pre-commit hook found")
        }
    }
}

configure<org.jlleitschuh.gradle.ktlint.KtlintExtension> {
    debug.set(false) // ì„±ëŠ¥ìƒ false ê¶Œì¥
    ignoreFailures.set(true)
    enableExperimentalRules.set(false)

    // ì„±ëŠ¥ í–¥ìƒì„ ìœ„í•œ ì¶”ê°€ ì„¤ì •
    android.set(false)
    outputToConsole.set(true)
    outputColorName.set("RED")
    verbose.set(false)

    reporters {
        reporter(ReporterType.CHECKSTYLE)
        reporter(ReporterType.JSON)
        reporter(ReporterType.HTML)
    }

    // ìƒì„±ëœ íŒŒì¼ë“¤ ì œì™¸
    filter {
        exclude("**/build/**")
        exclude("**/generated/**")
        exclude("**/*Q*.kt") // QueryDSL Qí´ë˜ìŠ¤ ì œì™¸
    }

    additionalEditorconfig.set(
        mapOf(
            "max_line_length" to "120",
            "ktlint_standard_no-wildcard-imports" to "enabled",
            // ì¶”ê°€ ê·œì¹™ë“¤
            "ktlint_standard_filename" to "disabled", // íŒŒì¼ëª… ê·œì¹™ ë¹„í™œì„±í™”
            "ktlint_standard_function-naming" to "disabled", // í•¨ìˆ˜ ì¹´ë©œ ì¼€ì´ìŠ¤ ê°•ì œ í•´ì œ
            "ktlint_standard_property-naming" to "disabled", // ì„¤ì • ê°’ ìŠ¤ë„¤ì´í¬ ê°•ì œ í•´ì œ
            "ktlint_standard_chain-wrapping" to "disabled", // ì²´ì´ë‹ ìŠ¤íƒ€ì¼ ê°•ì œ í•´ì œ
            "ktlint_standard_parameter-list-wrapping" to "disabled", // íŒŒë¼ë¯¸í„° ëª©ë¡ ìŠ¤íƒ€ì¼ ê°•ì œ í•´ì œ
        ),
    )
}

// ksp ì–´ë…¸í…Œì´ì…˜ í”„ë¡œì„¸ì‹±ì„ í†µí•´ QClass ë¹Œë“œí•˜ë„ë¡ ì„¤ì •
pluginManager.withPlugin("com.google.devtools.ksp") {
    kotlin {
        sourceSets.main {
            kotlin.srcDir(layout.buildDirectory.dir("generated/ksp/main/kotlin"))
        }
    }
}

// IntelliJ IDEA ì½”ë“œ ìŠ¤íƒ€ì¼ ì„¤ì • ë™ê¸°í™” íƒœìŠ¤í¬
tasks.register("applyKtlintToIdea") {
    group = "ide"
    description = "ktlint ê·œì¹™ë“¤ì„ ì¸í…”ë¦¬ì œì´ IDEA ì½”ë“œ ìŠ¤íƒ€ì¼ì— ì ìš©"
    dependsOn("ktlintApplyToIdea")
}
